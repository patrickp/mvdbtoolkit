SUBROUTINE MVDBTOOLKIT.WOBJ.OBJECT.TOSTRING(JSON,POS)
**********************************************************************
*
* Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
*
* Written by: Patrick Payne, Zumasys
* Date: December 2017
* Description: convert parsed json object node to string
*
**********************************************************************
*
INCLUDE MVDBTOOLKIT.WOBJ.INCLUDE
*
JSON:="{"; * START OUR STRING
INDENT.DEPTH+=1
*IF PRETTIFY THEN JSON:=NL
NUMBER.PARTS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,POS>
IF WOBJ.DEBUG THEN PRINT "WOBJ.TOSTRING: POS=":POS:" NUMBER.PARTS=":NUMBER.PARTS
*
* OPTION TO SORT THIS
*
*SORTTYPE="NSBASIC"
SORTTYPE=""
BEGIN CASE
     CASE 1
        KEYS=JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,POS>
        CONVERT @SVM TO @AM IN KEYS
        SORTED.KEYS=KEYS
END CASE
ACTUAL.KEYS=JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,POS>
FOR PP=1 TO NUMBER.PARTS
    PART.KEY=SORTED.KEYS<PP>
    LOCATE PART.KEY IN ACTUAL.KEYS<1,1> SETTING P ELSE
        IF WOBJ.DEBUG THEN PRINT "WOBJ.TOSTRING: COULD NOT LOCATE ":PART.KEY:" IN ":ACTUAL.KEYS
    END
    PART.TYPE=JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,POS,P>
    PART.KEY=JSONOBJECT(JSONOBJECT$NODEPARTS.KEY)<1,POS,P>
    IF WOBJ.DEBUG THEN PRINT "WOBJ.TOSTRING: POS=":POS:" P=":P:" T=":PART.TYPE:" K=":PART.KEY
    IF PP=1 AND PRETTIFY THEN JSON:=NL
    CALL MVDBTOOLKIT.WOBJ.ENCODE(PART.KEY,OUT.PART.KEY)
    CALL MVDBTOOLKIT.WOBJ.PRETTIFY.INDENT(JSON,POS)
    JSON:='"':OUT.PART.KEY:'": '
    BEGIN CASE
        CASE PART.TYPE="S"
            CALL MVDBTOOLKIT.WOBJ.STRING.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="B"
            CALL MVDBTOOLKIT.WOBJ.BOOLEAN.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="D"
            CALL MVDBTOOLKIT.WOBJ.NUMBER.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="N"
            CALL MVDBTOOLKIT.WOBJ.NULL.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="O"
            PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
            IF WOBJ.DEBUG THEN PRINT "WOBJ.TOSTRING: FL=":PART.FLINK
            CALL MVDBTOOLKIT.WOBJ.OBJECT.TOSTRING(JSON,PART.FLINK)
        CASE PART.TYPE="A"
            PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
            IF WOBJ.DEBUG THEN PRINT "WOBJ.TOSTRING: FL=":PART.FLINK
            CALL MVDBTOOLKIT.WOBJ.ARRAY.TOSTRING(JSON,PART.FLINK)
    END CASE
    IF PP <> NUMBER.PARTS THEN
        JSON:=", "
        IF PRETTIFY THEN JSON:=NL
    END
NEXT PP
*
IF PRETTIFY THEN
    JSON:=NL
    PASS.POS=POS-1
    CALL MVDBTOOLKIT.WOBJ.PRETTIFY.INDENT(JSON,PASS.POS)
END
JSON:="}"
*IF PRETTIFY THEN JSON:=NL
INDENT.DEPTH -= 1
RETURN
END
