SUBROUTINE MVDBTOOLKIT.WOBJ.ARRAY.TOSTRING(JSON,POS)
**********************************************************************
*
* Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
*
* Written by: Patrick Payne, Zumasys
* Date: December 2017
* Description: convert json array to string
*
**********************************************************************
*
INCLUDE MVDBTOOLKIT.WOBJ.INCLUDE
*
JSON:="["; * START OUR STRING
INDENT.DEPTH+=1
*IF PRETTIFY THEN JSON:=NL
NUMBER.PARTS=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,POS>
IF WOBJ.DEBUG THEN PRINT "ARRAY.TOSTRING: POS=":POS:" NUMBER.PARTS=":NUMBER.PARTS
FOR P=1 TO NUMBER.PARTS
    PART.TYPE=JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,POS,P>
    IF WOBJ.DEBUG THEN PRINT "ARRAY.TOSTRING: POS=":POS:" P=":P:" T=":PART.TYPE
    IF PRETTIFY AND P=1 THEN JSON:=NL
    CALL MVDBTOOLKIT.WOBJ.PRETTIFY.INDENT(JSON,POS)
    BEGIN CASE
        CASE PART.TYPE="S"
            CALL MVDBTOOLKIT.WOBJ.STRING.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="B"
            CALL MVDBTOOLKIT.WOBJ.BOOLEAN.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="D"
            CALL MVDBTOOLKIT.WOBJ.NUMBER.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="N"
            CALL MVDBTOOLKIT.WOBJ.NULL.TOSTRING(JSON,POS,P)
        CASE PART.TYPE="O"
            PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
            IF WOBJ.DEBUG THEN PRINT "OBJ FLINK=":PART.FLINK
            CALL MVDBTOOLKIT.WOBJ.OBJECT.TOSTRING(JSON,PART.FLINK)
        CASE PART.TYPE="A"
            PART.FLINK=JSONOBJECT(JSONOBJECT$NODEPARTS.FLINK)<1,POS,P>
            IF WOBJ.DEBUG THEN PRINT "FL=":PART.FLINK
            CALL MVDBTOOLKIT.WOBJ.ARRAY.TOSTRING(JSON,PART.FLINK)
    END CASE
    IF P <> NUMBER.PARTS THEN
        JSON:=", "
        IF PRETTIFY THEN JSON:=NL
    END
NEXT P
*
IF PRETTIFY AND NUMBER.PARTS THEN
    JSON:=NL
    CALL MVDBTOOLKIT.WOBJ.PRETTIFY.INDENT(JSON,POS)
END
JSON:="]"
*IF PRETTIFY THEN JSON:=NL
INDENT.DEPTH-=1
RETURN
END
