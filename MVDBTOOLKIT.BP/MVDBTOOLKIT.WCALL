      SUBROUTINE MVDBTOOLKIT.WCALL(MVDBTOOLKIT.WOBJ)
      **********************************************************************
      *
      * Copyright (C) 2018 Zumasys, Inc., All Rights Reserved
      *
      * Written by: Patrick Payne, Zumasys
      * Date: June 15, 2018
      * Description: WCALL Library

      **********************************************************************
      *
      * MVDBTOOLKIT.WOBJ
      * { "method":"GET,PUT,POST,ETC",
      *    "url":"URL TO CALL",
      *    "headers": { "HEADERNAME":"VALUE", "HEADERNAME":"VALUE" },
      *    "body":"BODY",
      *    "insecure":"Yes,Y,YES - Sets the -k on curl",
      *    "timeout": ## (default is 45)
      *    "response": {
      *        "result":"RESULT",
      *        "status":"STATUS",
      *        "http_type": "http type",
      *        "curl_cmnd":"Actual curl command created",
      *         "error": "error"
      *    }
      *  }

      CALL MVDBTOOLKIT.WPLATFORM(POBJ)

      CALL MVDBTOOLKIT.WOBJ(POBJ,"GET","mvtype",MV.TYPE,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(POBJ,"GET","platform",PLATFORM,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(POBJ,"GET","filedelim",FILE.DELIM,"",RERR)

      IF MV.TYPE="" THEN
         CALL MVDBTOOLKIT.WOBJ(POBJ,"GET","error",THIS.ERROR,"",RERR)
         PRINT "NO MV TYPE: ":THIS.ERROR
         RETURN
      END


      CALL MVDBTOOLKIT.WOBJ(POBJ,"GET","tmpdir",TMP.DIR,"",RERR)

      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","method",WEB.METHOD,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","url",WEB.URL,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","body",WEB.BODY,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","timeout",WEB.TIMEOUT,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"KEYS","headers",WEB.HEADERS,"",RERR)
      IF NOT(WEB.TIMEOUT) THEN WEB.TIMEOUT=45
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","insecure",WEB.INSECURE,"",RERR)
      IF OCONV(WEB.INSECURE[1,1],'MCU') = "Y" THEN WEB.INSECURE=1 ELSE WEB.INSECURE=0

      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.OBJECT","response","{}","",RERR)

      IF TMP.DIR="" THEN
         BEGIN CASE
            CASE PLATFORM="LINUX"
               TMP.DIR="/tmp"
            CASE INDEX(PLATFORM,"WINDOWS",1) AND MV.TYPE="JBASE"
               TMP.DIR="%HOME%"
            CASE INDEX(PLATFORM,"WINDOWS",1)
               TMP.DIR="%USERPROFILE%\AppData\Local\Temp"
            CASE 1
               TMP.DIR="/tmp"
         END CASE
      END

      CMND="curl -s"
      CMND:=' -m ':WEB.TIMEOUT
      CMND:=' -X ':WEB.METHOD

      WEB.NUM.HEADERS=DCOUNT(WEB.HEADERS,@SVM)

      FOR H=1 TO WEB.NUM.HEADERS
         HEADER.NAME=WEB.HEADERS<1,1,H>
         CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"GET","headers.":HEADER.NAME,HEADER.VALUE,"",RERR)
         CALL MVDBTOOLKIT.WOBJ.ENCODE(HEADER.VALUE,ENCODED.HEADER.VALUE)
         CMND:=' -H "':HEADER.NAME:': ':ENCODED.HEADER.VALUE:'"'
      NEXT H

      SESSION.ID="WCALL-":@USER.NO
      TMP.DIR:=FILE.DELIM

      * add in dumping of headers
      CMND:=" -D ":TMP.DIR:SESSION.ID:".headers"

      * write out stderr
      CMND:=" -v --stderr ":TMP.DIR:SESSION.ID:".stderr"

      IF WEB.BODY # "" THEN
         *CALL MVDBTOOLKIT.WOBJ(EOBJ,"SET.ARRAY","data","[]","",RERR)
         *CALL MVDBTOOLKIT.WOBJ(EOBJ,"SET.STRING","data[0]",WEB.BODY,"",RERR)


         CALL MVDBTOOLKIT.WOBJ(FOBJ,"FROMSTRING","","{}","",RERR)
         CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","action","WRITE","",RERR)
         CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","data",WEB.BODY,"",RERR)
         CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","path",TMP.DIR:SESSION.ID:'.body',"",RERR)

         CALL MVDBTOOLKIT.WFILEIO(FOBJ)
         CMND:=' --data-binary @':TMP.DIR:SESSION.ID:'.body'
         *CMND:='  --data-binary @-'
      END

      * NOW ADD THE URL

      WEB.URL=EXCHANGE(WEB.URL,' ','%20')
      WEB.URL=EXCHANGE(WEB.URL,'@','%40')
      WEB.URL=EXCHANGE(WEB.URL,'$','%24')
      *WEB.URL=EXCHANGE(WEB.URL,'/','%2F')

      CMND:=' "':WEB.URL:'"'

      * CALL WEXECUTE
      EOBJ="NEW"
      CALL MVDBTOOLKIT.WEXECUTE(EOBJ); * INITIATES OUR OBJECT
      CALL MVDBTOOLKIT.WOBJ(EOBJ,"SET","command",CMND,"",RERR)
      IF WEB.BODY # "" THEN
         * CALL MVDBTOOLKIT.WOBJ(EOBJ,"SET","data[0]",WEB.BODY,"",RERR)
      END

      CALL MVDBTOOLKIT.WEXECUTE(EOBJ); * DO THE CALL

      CALL MVDBTOOLKIT.WOBJ(FOBJ,"FROMSTRING","","{}","",RERR)
      CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","action","READ","",RERR)
      CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","path",TMP.DIR:SESSION.ID:".headers","",RERR)
      CALL MVDBTOOLKIT.WFILEIO(FOBJ)
      CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","response.data",RESPONSE.HEADERS,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.STRING","response.headers",RESPONSE.HEADERS,"",RERR)

      CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET","path",TMP.DIR:SESSION.ID:'.stderr',"",RERR)

      CALL MVDBTOOLKIT.WFILEIO(FOBJ)
      CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","response.data",RESPONSE.STDERR,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.STRING","response.stderr",RESPONSE.STDERR,"",RERR)

      * NEED TO GET FIRST < LINE AND PROCESS HTTP/1.1 200 OK
      LINE.CNTR=0
      NUM.LINES=DCOUNT(RESPONSE.STDERR,@AM)
      HTTP.TYPE=''; HTTP.STATUS.CODE=''

      LOOP
         LINE.CNTR+=1
         LINE=RESPONSE.STDERR<LINE.CNTR>
         IF LINE[1,1]='<' THEN
            HTTP.TYPE=FIELD(LINE,' ',2)
            HTTP.STATUS.CODE=FIELD(LINE,' ',3)
         END
      UNTIL LINE[1,1]='<' OR LINE.CNTR > NUM.LINES DO REPEAT

      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.NUMBER","response.status",HTTP.STATUS.CODE,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.STRING","response.http_type",HTTP.STATUS.CODE,"",RERR)

      *CALL MVDBTOOLKIT.WOBJ(EOBJ,"TOSTRING","",TEST.JSON,"PRETTIFY",RERR)
      CALL MVDBTOOLKIT.WOBJ(EOBJ,"GET","result.result",EXECUTE.RESULT,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.STRING","response.data",EXECUTE.RESULT,"",RERR)
end.of.call: *

      CALL MVDBTOOLKIT.WOBJ(MVDBTOOLKIT.WOBJ,"SET.STRING","response.curl_cmnd",CMND,"",RERR)

      RETURN


