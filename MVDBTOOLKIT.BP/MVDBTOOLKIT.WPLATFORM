      SUBROUTINE MVDBTOOLKIT.WPLATFORM(PLATFORMOBJ)
      **********************************************************************
      *
      * Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
      *
      * Written by: Patrick Payne, Zumasys
      * Date: June 12, 2018
      * Description: WPLATFORM OBJ
      *
      **********************************************************************
      *
      *
      * PLATFORMOBJ
      *
      * {
      *   "platform":" WINDOWS OR LINUX",
      *   "mvtype"  :" JBASE, D3, UNIDATA, UNIVERSE, OPENQM",
      *   "tmpdir"  :"tmp dir"
      * }

      USE.DICT=1
      OPEN "DICT","MVDBTOOLKIT.BP" TO DICT.MVDBTOOLKIT.BP ELSE USE.DICT=0

      IF USE.DICT THEN
            READ PLATFORMOBJ.JSON FROM DICT.MVDBTOOLKIT.BP, "MVDBTOOLKIT.PLATFORM.JSON" ELSE PLATFORMOBJ.JSON=""
      END
      
      OPEN "MD" TO FI.MD ELSE
            OPEN "VOC" TO FI.MD ELSE RETURN
      END
 
      IF PLATFORMOBJ.JSON="" THEN
            READ PLATFORMOBJ.JSON FROM FI.MD, "MVDBTOOLKIT.PLATFORM.JSON" ELSE
                  CALL WOBJ(PLATFORMOBJ,"FROMSTRING","","{}","",RERR)
                  CALL WOBJ(PLATFORMOBJ,"SET.STRING","error","No MVDBTOOLKIT.PLATFORM.JSON IN MD/VOC","",RERR)
                  CRT "No MVDBTOOLKIT.PLATFORM.JSON IN MD"
                  RETURN
            END
      END

      CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"FROMSTRING","",PLATFORMOBJ.JSON,"",RERR)
      
      * 9/2/2019 PAP - We now figure out filedelim vs passing it in original config filedelim

      CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"GET","platform",PLATFORM,"",RERR)
      CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"GET","mvtype",MV.TYPE,"",RERR)
      IF PLATFORM="WINDOWS" THEN DELIM = "\" ELSE DELIM = "/"
      CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"SET","filedelim",DELIM,"",RERR)

      CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"GET","tmpdir",TMP.DIR,"",RERR)

      * 11/10/2019 - Added default tmp dir, pulled from wcall function

      IF TMP.DIR="" THEN
            BEGIN CASE
                  CASE PLATFORM="LINUX"
                  TMP.DIR="/tmp"
                  CASE INDEX(PLATFORM,"WINDOWS",1) AND MV.TYPE="JBASE"
                  TMP.DIR="%HOME%"
                  CASE INDEX(PLATFORM,"WINDOWS",1)
                  TMP.DIR="%USERPROFILE%\AppData\Local\Temp"
                  CASE 1
                  TMP.DIR="/tmp"
            END CASE
            CALL MVDBTOOLKIT.WOBJ(PLATFORMOBJ,"SET","tmpdir",TMP.DIR,"",RERR)
      END

      RETURN
