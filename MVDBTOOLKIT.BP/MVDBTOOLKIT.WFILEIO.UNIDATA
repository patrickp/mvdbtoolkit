SUBROUTINE MVDBTOOLKIT.WFILEIO.UNIDATA(FOBJ)
**********************************************************************
*
* Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
*
* Written by: Patrick Payne, Zumasys
* Date: June 7th 2018
* Description: WFILEIO MULTI PLATFORM WORKING WITH O/S FILES
*
**********************************************************************
*
* INOBJ
*
* { "ACTION":"READ,WRTE,DELETE",
*   "PATH":"PATH TO THE FILE",
*   "DATA":"DATA FOR A WRITE",
*   "dosletter":"OPTIONAL DOS LETTER TO ADD TO PATH",
*   "NEWLINE":"CR,LF,CRLF,DOS,UNIX",
*   "PERMISSIONS":"TBD",
*   "debug":"Y or N",
*   "response": {
*       "data":"responsedata",
*       "status":1-ok, else no,
*       "statusmsg":"statusmsg" 
*   }
* }
*
* need to know the existing platform

*PLATFORM.DELIM="\"
*PLATFORM.TYPE="WINDOWS"
*PLATFORM.MV="D3"

CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","debug",DO.DEBUG,"",RERR)
IF OCONV(DO.DEBUG[1,1],"MCU") = "Y" OR DO.DEBUG="1" THEN DO.DEBUG=1 ELSE DO.DEBUG=""

CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.mvtype",PLATFORM.MV,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.filedelim",PLATFORM.DELIM,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.platform",PLATFORM.TYPE,"",RERR)

FSTATUS=1
FSTATUSMSG="OK"

CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","action",FILEIO.ACTION,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","path",FILEIO.PATH,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","data",FILEIO.DATA,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","dosletter",FILEIO.DOSLETTER,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","newline",FILEIO.NEWLINE,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","permissions",FILEIO.PERMISSIONS,"",RERR)

*CALL MVDBTOOLKIT.WOBJ(OUTOBJ,"FROMSTRING","","{}","",RERR)

FILEIO.ACTION=OCONV(FILEIO.ACTION,"MCU")
FILEIO.NEWLINE=OCONV(FILEIO.NEWLINE,"MCU")

CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.parsed_fileio_file_name",PARSED.FILEIO.FILE.NAME,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.fileio_just_path",FILEIO.JUST.PATH,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.platform",PLATFORM.TYPE,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.mvtype",PLATFORM.MVTYPE,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"GET","work.filedelim",PLATFORM.FILEDELIM,"",RERR)

* Lets create a q-pointer

USER.NO=FIELD(OCONV('','U50BB'),' ',1)
OPEN "VOC" TO FILE.VOC ELSE
    STOP "COULD NOT OPEN VOC"
END

MD.ID='MVDBTOOLKIT-QPOINTER-':USER.NO

MD.ENTRY='DIR'
MD.ENTRY<2>=FILEIO.JUST.PATH
WRITE MD.ENTRY ON FILE.VOC, MD.ID

OPEN MD.ID TO FI ELSE
   DELETE FILE.VOC, MD.ID
    FSTATUS=0
    FSTATUSMSG="Could not open file ":FILEIO.JUST.PATH
    GOTO end.of.run
END

BEGIN CASE
    CASE FILEIO.ACTION="READ"
        GOSUB fileio.read
    CASE FILEIO.ACTION="WRITE"
        GOSUB fileio.write
    CASE FILEIO.ACTION="DELETE"
        GOSUB fileio.delete
    CASE 1
        FSTATUS=0
        FSTATUSMSG="Invalid action"
        GOTO end.of.run
END CASE

CLOSE FI
DELETE FILE.VOC, MD.ID

end.of.run: *
CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET.NUMBER", "response.status", FSTATUS,"",RERR)
CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET.STRING", "response.statusmsg", FSTATUSMSG, "", RERR)


RETURN

fileio.read: *

REC=""

READ REC FROM FI, PARSED.FILEIO.FILE.NAME ELSE
    FSTATUS=0
    FSTATUSMSG="Could not find ":PARSED.FILEIO.FILE.NAME
END

CALL MVDBTOOLKIT.WOBJ(FOBJ,"SET.STRING","response.data",REC,"",RERR)

IF 0 THEN
    PRINT "PATH   :":FILEIO.JUST.PATH
    PRINT "ID     :":PARSED.FILEIO.FILE.NAME
    PRINT "REC    :":REC
END

RETURN

fileio.write: *

WRITE FILEIO.DATA ON FI, PARSED.FILEIO.FILE.NAME
FSTATUS=1

RETURN

fileio.delete: *

DELETE FI, PARSED.FILEIO.FILE.NAME
FSTATUS=1

RETURN
