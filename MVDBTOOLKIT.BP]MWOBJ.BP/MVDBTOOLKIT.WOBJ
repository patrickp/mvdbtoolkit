SUBROUTINE MVDBTOOLKIT.WOBJ(PASSEDOBJ,ACTION,NODE,VALUE,OPTIONS,RERR)
**********************************************************************
*
* Copyright (C) 2017 Zumasys, Inc., All Rights Reserved
*
* Written by: Patrick Payne, Zumasys
* Date: December 2017
* Description: WOBJ json library main routine
*
**********************************************************************
*
WOBJ.VERSION = "1.1"
*
INCLUDE MVDBTOOLKIT.WOBJ.INCLUDE
*
IF INDEX(OPTIONS,"PRETTIFY",1) THEN PRETTIFY=1 ELSE PRETTIFY=0
IF INDEX(OPTIONS,"PRETTIFYCR",1) THEN PRETTIFYCR=1 ELSE PRETTIFYCR=0
IF INDEX(OPTIONS,"DEBUG",1) THEN WOBJ.DEBUG=1 ELSE WOBJ.DEBUG=0
*
MAT JSONOBJECT = ""
RERR=""
INDENT.DEPTH=0
*
BEGIN CASE
    CASE ACTION="FROMSTRING"
* THIS EXISTS EARLIER DUE TO MATPARSE BELOW
        CALL MVDBTOOLKIT.WOBJ.PARSE(VALUE,NEWOBJ)
        PASSEDOBJ=NEWOBJ
        IF PSERRORS#"" THEN
            RERR=2
            RERR<2> = PSERRORS
        END
        IF WOBJ.DEBUG THEN PRINT "WOBJ: FROMSTRING ACTION: VALUE=":VALUE:" RERR=":RERR<2>
        RETURN
    CASE ACTION="VERSION"
        VALUE=WOBJ.VERSION
        RETURN
END CASE

RERR=''

* Doing inline include due to Assign/Unassigned (unidata)
* WOBJ.UNASSIGNED.INCLUDE

INCLUDE MVDBTOOLKIT.BP.WOBJ.BP.INCLUDE MVDBTOOLKIT.WOBJ.UNASSIGNED.INCLUDE

*IF NOT(ASSIGNED(PASSEDOBJ)) THEN RERR='PARAM1 BLANK'
*IF NOT(ASSIGNED(ACTION)) THEN RERR='PARAM2 (ACTION) NOT ASSIGNED'
*IF NOT(ASSIGNED(NODE)) THEN RERR='PARAM3 (NODE) NOT ASSIGNED'
*IF NOT(ASSIGNED(VALUE)) THEN VALUE=""
*IF NOT(ASSIGNED(OPTIONS)) THEN OPTIONS=""

IF RERR # '' THEN
   RERR<2>=RERR
   RERR<1>=1
   PRINT RERR<1>
   DEBUG
   RETURN
END

*
* MATPARSE JSONOBJECT FROM PASSEDOBJ; * all except unidata
MATPARSE JSONOBJECT FROM PASSEDOBJ, @AM; * Unidata
*
IF WOBJ.DEBUG THEN PRINT "ACTION=":ACTION
BEGIN CASE
    CASE ACTION="TOSTRING"
        VALUE=""
        IF NODE # "" THEN
            RERR=1
            RERR<2>="Node ignored - returning the root node"
            IF WOBJ.DEBUG THEN PRINT "WOBJ: TOSTRING ACTION: NODE ":NODE:" IGNORED"
        END
        CALL MVDBTOOLKIT.WOBJ.TOSTRING(VALUE)
    CASE ACTION="GET"
        GOSUB action.get
    CASE FIELD(ACTION,".",1)="SET"
        GOSUB action.set
    CASE ACTION="DELETE"
        GOSUB action.delete
    CASE ACTION="KEYS"
        GOSUB action.keys
    CASE ACTION="LENGTH"
        GOSUB action.length
    CASE ACTION="TYPE"
        GOSUB action.type
    CASE 1
        VALUE=""
        RERR=1
        RERR<2>="Invalid action '":ACTION:"'"
        IF WOBJ.DEBUG THEN PRINT "WOBJ: INVALID ACTION: ":ACTION
END CASE
*
MATBUILD PASSEDOBJ FROM JSONOBJECT
*
RETURN
*
action.get: *
CALL MVDBTOOLKIT.WOBJ.GET(ACTION,NODE,VALUE,RERR)
RETURN
*
action.set: *
CALL MVDBTOOLKIT.WOBJ.SET(ACTION,NODE,VALUE,RERR)
RETURN
*
action.delete: *
CALL MVDBTOOLKIT.WOBJ.DELETE(ACTION,NODE,VALUE,RERR)
RETURN
*
action.keys: *
CALL MVDBTOOLKIT.WOBJ.KEYS(ACTION,NODE,VALUE,RERR)
RETURN
*
action.length: *
CALL MVDBTOOLKIT.WOBJ.FINDNODE(NODE,NODEPOS,PARTPOS,CHILDPOS,LASTNODE,RERR)
IF PARTPOS THEN
    IF CHILDPOS THEN
        VALUE=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,CHILDPOS>+0
    END ELSE
        VALUE=1 ;* SIMPLE NODE HAS LENGTH OF 1
    END
END ELSE
    IF NODE="" THEN
        VALUE=JSONOBJECT(JSONOBJECT$NODELENGTH)<1,1>+0
    END ELSE
        VALUE=0 ;* NODE NOT FOUND!
    END
END
IF WOBJ.DEBUG THEN PRINT "WOBJ: LENGTH ACTION: ":NODE:" NODEPOS=":NODEPOS:" PARTPOS=":PARTPOS:" CHILDPOS=":CHILDPOS:" LENGTH=":VALUE:" RERR=":RERR<2>
RETURN
*
action.type: *
CALL MVDBTOOLKIT.WOBJ.FINDNODE(NODE,NODEPOS,PARTPOS,CHILDPOS,LASTNODE,RERR)
IF PARTPOS THEN
    PARTTYPE = JSONOBJECT(JSONOBJECT$NODEPARTS.TYPE)<1,NODEPOS,PARTPOS>
    BEGIN CASE
        CASE PARTTYPE='O'
            VALUE='OBJECT'
        CASE PARTTYPE='A'
            VALUE='ARRAY'
        CASE PARTTYPE='D'
            VALUE='NUMBER'
        CASE PARTTYPE='S'
            VALUE='STRING'
        CASE PARTTYPE='N'
            VALUE='NULL'
        CASE PARTTYPE='B'
            VALUE='BOOLEAN'
        CASE 1
            VALUE='UNKNOWN'
    END CASE
END ELSE
    IF NODE="" THEN
        VALUE="OBJECT"
    END ELSE
        VALUE="UNDEFINED" ;* NODE NOT FOUND!
    END
END
IF WOBJ.DEBUG THEN PRINT "WOBJ: TYPE ACTION: ":NODE:" NODEPOS=":NODEPOS:" PARTPOS=":PARTPOS:" CHILDPOS=":CHILDPOS:" TYPE=":VALUE:" RERR=":RERR<2>
RETURN
*
END
